apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Required when setting minSdkVersion to 20 or lower
        multiDexEnabled true
        // need to specify this to prevent junit runner from going deep into our dependencies
        testInstrumentationRunnerArguments = ['package': 'com.google.fhirengine']
    }
    sourceSets {
        // shared code between junit and instrumentation tests
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
        test {
            resources.srcDirs += ['sampledata']
        }
        androidTest {
            resources.srcDirs += ['sampledata']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

configurations {
    all {
        exclude module: 'json'
        exclude module: 'xpp3'
        exclude module: 'hamcrest-all'
        exclude module: 'jaxb-impl'
        exclude module: 'jaxb-core'
        exclude module: 'jakarta.activation-api'
        exclude module: 'javax.activation'
        exclude module: 'jakarta.xml.bind-api'
        // TODO: the following line can be removed from the next CQL engine release.
        exclude  module: 'hapi-fhir-jpaserver-base'
    }
}

dependencies {
    api ("ca.uhn.hapi.fhir:hapi-fhir-structures-r4:$hapiFhirStructuresR4Version") {
        exclude module:'junit'
    }
    api "org.opencds.cqf:cql-engine:$cqlEngineVersion"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    api "androidx.work:work-runtime-ktx:$workManagerVersion"
    implementation "com.google.guava:guava:$guavaVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.opencds.cqf:cql-engine-fhir:$cqlEngineVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    testImplementation "androidx.test:core:$testVersion"
    testImplementation "org.robolectric:robolectric:$roboelectricVersion"
    testImplementation "androidx.lifecycle:lifecycle-livedata-core-ktx:$liveDataVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "com.google.truth:truth:$truthVersion"
    androidTestImplementation "androidx.test.ext:junit-ktx:$androidTestJunitVersion"
    androidTestImplementation "androidx.test:runner:$testVersion"
    androidTestImplementation "androidx.test:core:$testVersion"
    androidTestImplementation "junit:junit:$junitVersion"
    androidTestImplementation "com.google.truth:truth:$truthVersion"
}

tasks.withType(Test) {
    testLogging {
        // to see the exceptions of failed tests in Github Actions console.
        exceptionFormat = 'full'
    }
    maxHeapSize = "2g"
}

repositories {
    mavenCentral()
}
